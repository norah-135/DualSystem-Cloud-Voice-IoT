#include <WiFiS3.h>
#include <ArduinoJson.h>

const char* ssid = "Nader-4G";
const char* password = "00447711";

WiFiServer localServer(80);
const int ledPin = 5;

void setup() {
  pinMode(ledPin, OUTPUT);
  digitalWrite(ledPin, LOW);
  Serial.begin(115200);
  while (!Serial); 
  showMenu();
}

void loop() {
  if (Serial.available()) {
    char input = Serial.read();
    switch (input) {
      case '0': showMenu(); break;
      case '1': connectToWiFi(); break;
      case '2': showSystemInfo(); break;
      default:
        if (input != '\n' && input != '\r') {
          Serial.println("Invalid command.");
        }
        break;
    }
  }

  if (WiFi.status() == WL_CONNECTED) {
    WiFiClient client = localServer.available();
    if (client) {
      handleIncomingClient(client);
    }
  }
}

void showMenu() {
  Serial.println("Menu:");
  Serial.println("[0] Show Menu");
  Serial.println("[1] Connect WiFi");
  Serial.println("[2] System Info");
}

void connectToWiFi() {
  if (WiFi.status() == WL_CONNECTED) return;
  WiFi.begin(ssid, password);
  int counter = 0;
  while (WiFi.status() != WL_CONNECTED && counter < 10) {
    delay(1000);
    counter++;
  }
  if (WiFi.status() == WL_CONNECTED) {
    localServer.begin();
    showSystemInfo();
  }
}

void showSystemInfo() {
  if (WiFi.status() == WL_CONNECTED) {
    Serial.print("IP: ");
    Serial.println(WiFi.localIP());
  }
}

void handleIncomingClient(WiFiClient client) {
  String fullRequest = "";
  unsigned long timeout = millis();
  while (client.connected() && millis() - timeout < 2000) {
    while (client.available()) {
      fullRequest += (char)client.read();
      timeout = millis();
    }
  }
  int jsonStart = fullRequest.indexOf('{');
  int jsonEnd = fullRequest.lastIndexOf('}');
  if (jsonStart != -1 && jsonEnd != -1) {
    processJson(fullRequest.substring(jsonStart, jsonEnd + 1));
  }
  client.println("HTTP/1.1 200 OK\r\nConnection: close\r\n\r\n");
  client.stop();
}

void processJson(String json) {
  StaticJsonDocument<512> doc;
  deserializeJson(doc, json);
  String transcription = doc["transcription"] | "";
  String action = doc["action"] | "";
  if (transcription.indexOf("ولع") >= 0 || action == "on") {
      digitalWrite(ledPin, HIGH);
  } else if (transcription.indexOf("طفي") >= 0 || action == "off") {
      digitalWrite(ledPin, LOW);
  }
}
